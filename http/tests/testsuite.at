# Process with autom4te to create an -*- Autotest -*- test suite.

AT_INIT([bes/http/tests testsuite])

dnl m4_include([modules/handler_tests_macros.m4])

# Start out with these test macros defined here, then migrate to the common file

# run the same command in parallel - two processes
m4_define([AT_BESCMD_PARALLEL_TEST], [dnl

    AT_SETUP([$1])
    AT_KEYWORDS([bescmd])

    input=$1
    dnl baseline=$abs_srcdir/$1.baseline
    bes_conf=$abs_srcdir/bes.conf

    # AT_XFAIL_IF is always run first regardless where it appears. jhrg 5/27/22
    AT_XFAIL_IF([test z$2 = zxfail])

    AS_IF([test -z "$at_verbose"], [echo "COMMANDS: remote_resource_tester $bes_conf $input &"])

    dnl Clean up the cache
    rm -rf cache

    remote_resource_tester $bes_conf $input &
    p1=$!
    AS_IF([test -z "$at_verbose"], [echo "Started 1: $p1"])

    remote_resource_tester $bes_conf $input &
    p2=$!
    AS_IF([test -z "$at_verbose"], [echo "Started 2: $p2"])

    wait $p1
    s1=$?
    wait $p2
    s2=$?
    AT_CHECK([test $s1 -eq 0], [0])
    AT_CHECK([test $s2 -eq 0], [0])

    AT_CLEANUP
])

AT_BESCMD_PARALLEL_TEST([http://test.opendap.org/uat_tester_eval_files/chunked_oneD.h5])
