# Process with autom4te to create an -*- Autotest -*- test suite.

AT_INIT([bes/http/tests testsuite])

m4_include([modules/handler_tests_macros.m4])

# Start out with these test macros defined here, then migrate to the common file

# run the same command in parallel - two processes
m4_define([AT_BESCMD_PARALLEL_TEST], [dnl

    AT_SETUP([$1])
    AT_KEYWORDS([bescmd])

    input=$abs_srcdir/$1
    baseline=$abs_srcdir/$1.baseline
    # AT_XFAIL_IF is always run first regardless where it appears. jhrg 5/27/22
    AT_XFAIL_IF([test z$2 = zxfail])

    AS_IF([test -z "$at_verbose"], [echo "COMMANDS: besstandalone -c $bes_conf -i $1 2>&1 > tmp_n &"])

    AS_IF([test -n "$baselines" -a x$baselines = xyes],
        [
        besstandalone -c bes.conf -i $input 2>&1 > tmp1 &
        p1=$!
        AS_IF([test -z "$at_verbose"], [echo "Started 1: $p1"])

        besstandalone -c bes.conf -i $input 2>&1 > tmp2 &
        p2=$!
        AS_IF([test -z "$at_verbose"], [echo "Started 2: $p2"])

        AT_CHECK([wait $p1], [0], [], [], [s1=$!], [s1=$!])
        AT_CHECK([wait $p2], [0], [], [], [s1=$!], [s1=$!])

        mv tmp1 $baseline
        ],
        [
        besstandalone -c bes.conf -i $input 2>&1 > tmp1 &
        p1=$!
        AS_IF([test -z "$at_verbose"], [echo "Started 1: $p1"])

        besstandalone -c bes.conf -i $input 2>&1 > tmp2 &
        p2=$!
        AS_IF([test -z "$at_verbose"], [echo "Started 2: $p2"])

        AT_CHECK([wait $p1], [0], [], [], [s1=$!], [s1=$!])
        AT_CHECK([wait $p2], [0], [], [], [s1=$!], [s1=$!])

        AT_CHECK([diff -b -B $baseline tmp1])
        AT_CHECK([diff -b -B $baseline tmp2])
        ])

    AT_CLEANUP
])

