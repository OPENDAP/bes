
dnl -*- m4 -*-
dnl Process this file with autoconf to produce a configure script.

AC_REVISION($Id: configure.in,v 1.25 2004/07/09 18:08:50 jimg Exp $)
AC_INIT(hdfclass)
AC_CONFIG_AUX_DIR(../../etc)
AC_CONFIG_HEADER(config_hdf.h)

AC_PREFIX_DEFAULT("\$(DODS_ROOT)")

dnl DODS_ROOT is the fully qualified pathname to the root of the DODS
dnl subtree. 

DODS_GET_DODS_ROOT
DODS_DIRECTORY_VERSION

DODS_SERVER_VERSION=\"hdf-dods/$VERSION\"
AC_DEFINE_UNQUOTED(DODS_SERVER_VERSION, $DODS_SERVER_VERSION)

dnl `depend' is the command to use to generate dependencies for Makefiles.

depend=${dods_relative}/etc/depend.sh
AC_SUBST(depend)

AC_CANONICAL_HOST
AC_SUBST(host)

# Until further notice OS X port doesn't get -gstabs because of an
# apparent bug in their gcc 3.3 clone (a beta).  If you see this after
# 1/1/2005, you can probably remove it.
#
# For the alpha/osf, the hdf server handler must be linked statically. This 
# is because the OSF linker (GNU ld is not supportted for this platform)
# looks for .so libraries first and osf ships with a jpeg.so that will not
# work with the HDF4 libraries. Using -static forces only .a archives in
# the link which, in turn, causes ld to pick jpeg.a from /usr/local/hdf/lib.
# jhrg 6/30/04
case "${host_cpu}:${host_vendor}:${host_os}" in
     powerpc:apple:netbsd*)
       CFLAGS=${CFLAGS-"-O2 $CFLAGS"}
       CXXFLAGS=${CXXFLAGS-"-O2 $CXXFLAGS"}
       break;;
     alphaev56:dec:osf5.*)
       CFLAGS=${CFLAGS-"-O2 $CFLAGS"}
       CXXFLAGS=${CXXFLAGS-"-O2 $CXXFLAGS"}
       LDFLAGS="-static $LDFLAGS"
       break;;
    *:*:*)
       CFLAGS=${CFLAGS-"-gstabs -O2 $CFLAGS"}
       CXXFLAGS=${CXXFLAGS-"-gstabs -O2 $CXXFLAGS"}
       break;;
esac    

dnl Checks for programs.

AC_PROG_AWK
AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_CPP
AC_PROG_INSTALL

DODS_GCC
DODS_PROG_LEX
DODS_PROG_GTAR
DODS_PROG_PERL
DODS_PROG_BISON

dnl Never use exceptions with this code until 2.8.x is out!

DODS_OS
DODS_MACHINE
if test "$OS" = ultrix && test "$MACHINE" = vax; then
    case "$CC" in
        /bin/cc*|cc*)
echo "changing C compiler to \`vcc' because \`cc' floating-point is broken"
            CC=vcc
            ;;
    esac
fi

CXX=${CXX-g++}
AC_MSG_RESULT(Setting CXX to $CXX)
AC_SUBST(CXX)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h sys/param.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for specific libraries
AC_CHECK_LIB(pthread, pthread_testcancel, 
	[LIBS="$LIBS -lpthread"
	 AC_DEFINE(_REENTRANT)])

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strerror strtol)

DODS_CHECK_SIZES

DODS_HDF_LIBRARY

DODS_RX_LIB
DODS_WWW_LIB
DODS_COMPRESSION_LIB

DODS_EFENCE
DODS_DBNEW
DODS_DEBUG_OPTION

dnl check for an explict server cache pathname. If so, use that. Otherwise
dnl use /usr/tmp.

AC_ARG_WITH(cache_path,
    [  --with-cache=DIR        Server's cache (default: /usr/tmp)],
    HDF_CACHE=${withval}, HDF_CACHE=/usr/tmp)

AC_MSG_RESULT(Setting the HDF server cache directory to $HDF_CACHE)
AC_SUBST(HDF_CACHE)

dnl setup the url for the testsuite.
if test -f tests.conf
then
    default_url=`grep 'url:' tests.conf | sed 's/url:\(.*\)$/\1/'`
else
    default_url=http://localhost/dods-test/nph-dods/data/hdf/server-tests/
fi

AC_ARG_WITH(test-url,
    [  --with-test-url=url     Test file base URL (default: http://localhost/dods-test/nph-dods/data/hdf/server-tests/)],
    testurl=${withval}, testurl=$default_url)

AC_MSG_RESULT(Setting the Test file base URL to $testurl)
AC_SUBST(testurl)

AC_OUTPUT(Makefile hdfclass/Makefile hdf-testsuite/geturl.0/basic.exp
	hdf-testsuite/geturl.0/hdfeos.exp)
